[export:yes]
[integer64:yes]
(i64[] a)main(i32? res)
{
    (a, 0, ?a)sort(tmpR);
    i64[] tmp;
    tmp <- ?tmpR;
    i64 x, sum;
    sum <- x <- 0;
    while x < ?tmp
    {
        sum <- sum + tmp[x] * x;
        x <- x + 1;
    }
    res <- (i32)sum;
}
 


[integer64:yes]
(i64[] a, i64[] b)merge(i64[]? ret)
{
    i64 alen, blen, i, j;
    alen <- ?a;
    blen <- ?b;
    i64[] res;
    res <- new i64[](alen + blen);
    i <- j <- 0;
    while i < alen && j < blen
    {
        match a[i] < b[j]
        {
            (i32)-1 {
                res[i + j] <- a[i];
                i <- i + 1;
            }
            (i32)0 {
                res[i + j] <- b[j];
                j <- j + 1;
            }
        }
    }
    while i < alen
    {
        res[i + j] <- a[i];
        i <- i + 1;
    }
    while j < blen
    {
        res[i + j] <- b[j];
        j <- j + 1;
    }
    ret <- res;
}


[integer64:yes]
(i64[] a, i64 l, i64 r)sort(i64[]? res)
{
    match (r - l) <= 1
    {
        (i32)-1 {
            i64[] x;
            x <- new i64[](1);
            x[0] <- a[l];
            res <- x;
        }
        (i32)0 {
            i64 m;
            m <- (l + r) / 2;
            (a, l, m)sort(t1);
            (a, m, r)sort(t2);
            (?t1, ?t2)merge(tmp);
            res <- ?tmp;
        }
    }
}


